/* https://github.com/bintray/gradle-bintray-plugin */

apply plugin: 'maven-publish'

publishing {
    publications {
        EligiblePublication(MavenPublication) {
            from components.java

            groupId group
            artifactId archivesBaseName
            version version

            pom.withXml {
                def root = asNode()

                root.appendNode('name',         'eligible-java')
                root.appendNode('description',  'Java bindings for Eligible APIs (https://eligible.com).')
                root.appendNode('url',          'https://github.com/eligible/eligible-java')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/eligible/eligible-java')
                scm.appendNode('connection', 'git@github.com:eligible/eligible-java.git')
                scm.appendNode('developerConnection', 'git@github.com:eligible/eligible-java.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', 'https://opensource.org/licenses/MIT')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'eligible')
                developer.appendNode('name', 'Eligible Engineering')
                developer.appendNode('email', 'engineering@eligible.com')
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['EligiblePublication']
    pkg {
        repo = 'maven'
        name = archivesBaseName
        userOrg = user
        desc = 'Java bindings for Eligible APIs (https://eligible.com).'
        websiteUrl = 'https://github.com/eligible/eligible-java'
        issueTrackerUrl = 'https://github.com/eligible/eligible-java/issues'
        vcsUrl = 'https://github.com/eligible/eligible-java.git'
        licenses = ['MIT']

        version {
            name = '1.3.0'
            desc = 'Eligible Java Bindings'
            released = new Date()
            vcsTag = '1.3.0'
            attributes = ['eligible-java': 'com.eligible:eligible-java']

            //Optional configuration for Maven Central sync of the version
            mavenCentralSync {
                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = project.hasProperty('ossUser') ? project.property('ossUser') : System.getenv('OSS_USER')
                password = project.hasProperty('ossPass') ? project.property('ossPass') : System.getenv('OSS_USER')
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central.
                // You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

// RUN ./gradlew bintrayUpload
