/* https://github.com/bintray/gradle-bintray-plugin */

apply plugin: 'maven-publish'

def description = 'Java bindings for Eligible APIs (https://eligible.com).'
def repoUrl = 'https://github.com/eligible/eligible-java'

def getConfig(envVarName, propName) {
    if (System.getenv(envVarName)) {
        return System.getenv(envVarName)
    } else if (project.hasProperty(propName)) {
        return project.property(propName)
    }
}

publishing {
    publications {
        EligiblePublication(MavenPublication) {
            from components.java

            groupId project.group
            artifactId project.archivesBaseName
            version project.version

            pom.withXml {
                def root = asNode()

                root.appendNode('name', project.archivesBaseName)
                root.appendNode('description', description)
                root.appendNode('url', repoUrl)

                def scm = root.appendNode('scm')
                scm.appendNode('url', repoUrl)
                scm.appendNode('connection', 'git@github.com:eligible/eligible-java.git')
                scm.appendNode('developerConnection', 'git@github.com:eligible/eligible-java.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', 'https://opensource.org/licenses/MIT')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'eligible')
                developer.appendNode('name', 'Eligible Engineering')
                developer.appendNode('email', 'engineering@eligible.com')
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = getConfig('BINTRAY_USER', 'bintrayUser')
    key = getConfig('BINTRAY_API_KEY', 'bintrayApiKey')

    publications = ['EligiblePublication']
    pkg {
        repo = 'maven'
        name = project.archivesBaseName
        userOrg = user
        desc = description
        websiteUrl = repoUrl
        issueTrackerUrl = 'https://github.com/eligible/eligible-java/issues'
        vcsUrl = 'https://github.com/eligible/eligible-java.git'
        licenses = ['MIT']

        version {
            name = project.version
            desc = description
            released = new Date()
            vcsTag = project.version
            attributes = ['eligible-java': 'com.eligible:eligible-java']

            //Optional configuration for Maven Central sync of the version
            mavenCentralSync {
                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = getConfig('OSS_USER', 'ossUser')
                password = getConfig('OSS_PASS', 'ossPass')
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central.
                // You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

// RUN ./gradlew bintrayUpload
